// TODO ..@condition should be parsed as an expression instead of a wildcard
syntax statement("if" ..@condition {}@then_branch) {
  () = if condition() then { then_branch(); () } else {}
}

// TODO ..@condition should be parsed as an expression instead of a wildcard
syntax statement("if" ..@condition {}@then_branch "else" {}@else_branch) {
  () = if condition() then { then_branch(); () } else { else_branch(); () }
}

// TODO think of a better solution
// It is a bit unfortunate that we have to define this wrapper,
// otherwise we match the block version of this which will fail
// parsing (@condition() then) as an expression
//syntax statement("if" ..@condition "then" {}@then_branch "else" {}@else_branch) (
  //if condition() then then_branch() else else_branch()
//)

syntax statement("while" ..@condition {}@body) {
  label _loop_start;
  label placeholder _loop_end;

  syntax statement("break") { goto _loop_end; }
  syntax statement("continue") { goto _loop_start; }
  if condition() {
    body(@scope);
    goto _loop_start;
  }
  label _loop_end;
}

import :: macro (file_path : String) -> (Scope) {
  mass_import(@context, file_path)
}

syntax statement("for" ..@pre ";" ..@condition ";" ..@post {}@body) {
  pre();
  label _loop_start;
  label placeholder _loop_end;
  syntax statement("break") { goto _loop_end; }
  syntax statement("continue") { goto _loop_start; }
  if condition() {
    body(@scope);
    post();
    goto _loop_start;
  }
  label _loop_end;
}

syntax statement("debugger") { inline_machine_code_bytes(0xCC) }


syntax statement("goto" .@label) { inline_machine_code_bytes(0xE9, label()) }

bool :: s8
true :: 1
false :: 0

operator 16 (& x) { address_of(x) }

operator 15 (x << y) { logical_shift_left(x, y) }
operator 15 (x | y) { bitwise_or(x, y) }
operator 15 (x & y) { bitwise_and(x, y) }

syntax statement rewrite("const" ..@binding "=" ..@expression) binding := expression
syntax statement rewrite("mut" ..@binding "=" ..@expression) binding := expression
syntax statement rewrite("stack" .@name ..@type) name : type
syntax statement rewrite("static" ..@binding "=" ..@expression) binding :: expression












